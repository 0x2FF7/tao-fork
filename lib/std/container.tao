#! Utilities for manipulating and traversing containers.

import "math.tao"

class Map B, e =
    => Item
    => Output
    => map : (Self.Item -> e ~ B) -> Self -> e ~ Self.Output

def map V < Map B e, B, e : (V.Item -> e ~ B) -> V -> e ~ V.Output = V.map

class Iter =
    => Item
    => next : Self -> Maybe (Self.Item, Self)

fn fold A, I < Iter, e : A -> (A -> I.Item -> e ~ A) -> I -> e ~ A = a, f, xs => match I.next(xs) in
    | None => a
    \ Just (x, xs) => fold(f(a, x)!, f, xs)!

def sum C, A : C -> A where
    C < Iter with { Item = A },
    A < Zero + Add A with { Output = A },
= fold(A.zero, fn x, y => x + y)

def product C, A : C -> A where
    C < Iter with { Item = A },
    A < One + Mul A with { Output = A },
= fold(A.one, fn x, y => x * y)
